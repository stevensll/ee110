; utilities
 .include  "inc/GeneralMacros.inc"
 .include  "inc/GeneralConstants.inc"

; CC26x2 hardware
 .include  "inc/CPUreg.inc"
 .include  "inc/GPIOreg.inc"
 .include  "inc/IOCreg.inc"
 .include  "inc/GPTreg.inc"
 .include  "inc/KeypadDemo.inc"
 
        .data
        .align 4
        ; the event queue, NOTE: for this demo, it is just a dummy buffer
eventQueue:         .space  QUEUE_SIZE * BYTES_PER_WORD
      ; variables
queueIndex:             .space          BYTES_PER_WORD



    .text
    .def EnqueueEvent
    .def InitEventQueue
; InitEventQueue
; Description:       Initializes the event queue. Note that this is a "dummy"
;                    event queue implemented as a buffer, so all it does is 
;                    reset the index to the buffer. By default, the queue
;                    values are initializaed to 0 when declare with
;                    the ".space" directive.
;           
; Operation:         The queueIndex variable is reset to 0, which is the
;                    start of the event queue.
;
; Arguments:         None.
; Return Value:      None.
;
; Local Variables:   None.
; Shared Variables:  queueIndex  (W): the key index is reset to 0
; Global Variables:  None.
;
; Input:             None.
; Output:            None.
;
; Error Handling:    None.
;
; Algorithms:        None.
; Data Structures:   None.
;
; Registers Changed: R0, R1
; Stack Depth:       0 words
;
; Revision History:  11/27/25   Steven Lei       initial revision

InitEventQueue:
        MOVA    R1, queueIndex              ;get the index from memory
        MOV     R0, #0                      
        STR     R0, [R1]                    ;and reset it (0 indexed)

        BX      LR                          ;done so return


; EnqueueEvent
; Description:       Adds an event (argument) into the event queue. Note that
;                    this event queue is implemented as a buffer, so all it 
;                    does is write the event to the buffer at the current
;                    queue index. It then increments the queue index.
;                    NOTE: If the queue is full, the queue will OVERWRITE 
;                    the next value by wrapping the queue index back to start.
;           
; Operation:         The event is stored in the queue and the queue index
;                    is then incremented. The queue index is then reset to 0
;                    if it exceeds the queue size (wrap around).
;
; Arguments:         R0: the event to add to the queue
; Return Value:      None.
;
; Local Variables:   None.
; Shared Variables:  queueIndex (R, W): the queue index is read to determine
;                                       where to write to the queue and updated
;                                       to point to the next writeable entry
;                    eventQueue (W):    the event is written to the queue
; Global Variables:  None.
;
; Input:             None.
; Output:            None.
;
; Error Handling:    None.
;
; Algorithms:        None.
; Data Structures:   None.
;
; Registers Changed: R0, R1, R2, R3, R4
; Stack Depth:       0 words
;
;
; Revision History:  11/27/25   Steven Lei       initial revision

EnqueueEvent:

AddEventToQueue:                        ;write the event (R0) to the queue 
        MOVA    R3, queueIndex              ;get the queue index from memory 
        LDR     R2, [R3]                    

        MOV     R4, #BYTES_PER_WORD         ;scale queue index to create offset
        MUL     R4, R2, R4                  ;R4 is now byte offset to the queue 

        MOVA    R1, eventQueue              ;get base addr for event queue 
        STR     R0, [R1, R4]                ;and store event at base + offset

UpdateQueueIndexValue:                  ;update the queue index with wraparound
        ADD     R2, R2, #1                  ;increment queue index
        CMP     R2, #QUEUE_SIZE             ;check if index is past end of queue
        BNE     DoneEnqueueEvent            ;   within bounds, so done
        MOV     R2, #0                      ;past end, so wrap back to start

DoneEnqueueEvent:                       
        STR     R2, [R3]                    ;write new queue index to memory
        BX      LR                          ;done so return
